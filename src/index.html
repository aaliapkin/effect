<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="utf-8" />
  <!-- <link rel="icon" href="%PUBLIC_URL%/favicon.ico" /> -->
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <title>Mandelbruh</title>
  <link rel="stylesheet" href="styles.css">
  <link href="https://fonts.googleapis.com/css2?family=Lato:wght@300;400&display=swap" rel="stylesheet">
</head>

<body>
  <noscript>You need to enable JavaScript to view this site.</noscript>

  <script id="vertexShader" type="x-shader/x-vertex">#version 300 es
    precision highp float;
    layout(location = 0) in vec2 aPos;

    out vec2 uv;

    uniform mat4 u_MVP;

    void main() {
      gl_Position = vec4(aPos, 0.0, 1.0);
      vec4 uv_out =  inverse(u_MVP) * gl_Position;
      uv = uv_out.xy;
    }
    
  </script>

  <script id="fragmentShader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    #define MAX_ITERATIONS 255.0
    #define S(a, b, t) smoothstep(a, b, t)
    
    out vec4 FragColor;
    in vec2 uv;
    
    uniform vec4 u_bounds;
    uniform float u_time;
    uniform float u_Size;
    uniform sampler2D u_Sampler;
    
    float map(float value, float min1, float max1, float min2, float max2) {
        return min2 + (value - min1) * (max2 - min2) / (max1 - min1);
    }

    float N21(vec2 p) {
      p = fract(p * vec2(123.34f, 345.45f));      
      p += dot(p, p + 34.345f);
      return fract(p.x * p.y);      
    }

    vec3 Layer(vec2 UV, float t) {
      
      vec2 asp = vec2(2.0f, 1.0f); // y: 2, x: 1
      vec2 uv1 = UV * u_Size * asp;
      uv1.y = -uv1.y;
      uv1.y += t * .25f;
      vec2 gv = fract(uv1) - .5f;
      vec2 id = floor(uv1);

      float n = N21(id);
      t += n * 6.2831;
      
      float w = UV.y * 10.0f;
      float x = (n - 0.5f) * .8f;
      x += (.4f - abs(x)) * sin(3.0f * w) * pow(sin(w), 6.0f) * .45f;

      float y = -sin(t + sin(t + sin(t) * .5f)) * .45f;
      y -= (gv.x - x) * (gv.x - x);

      vec2 dropPos = (gv - vec2(x, y)) / asp;
      float drop = S(.035f, .025f, length(dropPos));

      vec2 trailPos = (gv - vec2(x, t * .25f)) / asp;
      trailPos.y = (fract(trailPos.y * 20.0f) - 0.5f) / 20.0f;
      float trail = S(.02f, .01f, length(trailPos));
      float fogTrail = S(-.05f, .05f, dropPos.y);
      fogTrail *= S(.6f, y, gv.y);
      trail *= fogTrail;
      fogTrail *= S(0.03f, 0.02f, abs(dropPos.x));

      vec2 offs = drop * dropPos + trail * trailPos;

      return vec3(offs, fogTrail);
    }

    void main()
    {
      float Pi = 6.28318530718;

      FragColor = vec4(0.0f);
      float t = mod(u_time, 72000.0f);

      vec3 drops = Layer(uv, t);
      drops += Layer(uv * 1.73f + 1.75f, t + 1.87);

      vec2 offs = drops.xy;

      float blur = (1.0f - drops.z);
      
      float Directions = 8.0;
      float Quality = 3.0; 
      float Size = 8.0;
      vec2 Radius = vec2(.02f);

      vec2 uvoff = uv + offs + vec2(0.5f, 0.5f);
      vec4 Color = texture(u_Sampler, uvoff, 4.0f);

      for(float d = 0.0; d < Pi; d += Pi/Directions) {
        for(float i = 1.0 / Quality; i <= 1.0; i += 1.0 / Quality) {
          Color += texture(u_Sampler, uvoff + vec2(cos(d), sin(d)) * Radius * i, 4.0f);		
        }
      }

      Color /= Quality * Directions;
      vec4 BaseColor = texture(u_Sampler, uvoff);
      FragColor = mix(BaseColor, Color, blur);
    }

  </script>

  <script src="./script.js"></script>

</body>

</html>